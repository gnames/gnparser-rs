SciName = { SOI ~ Name ~ Tail ~ EOI }

Name = _{ SingleName }

Tail = { ((Sp | ";" | ",") ~ ANY*)? }

SingleName = { NameUninomial }

NameUninomial = _{ UninomialCombo | Uninomial }

SubGenusOrSuperspecies = _{ "(" ~ Sp? ~ NameLowerChar+ ~ Sp? ~ ")" }

SubGenus = { "(" ~ Sp? ~ UninomialWord ~ Sp? ~ ")" }

UninomialCombo = { UninomialCombo1 | UninomialCombo2 }
UninomialCombo1 = { UninomialWord ~ Sp? ~ SubGenus ~ (Sp? ~ Authorship)? }
UninomialCombo2 = { Uninomial ~ Sp ~ RankUninomial ~ Sp ~ Uninomial }

RankUninomial = { RankUninomialPlain | RankUninomialNotho }

RankUninomialPlain = _{ ("sect" | "subsect" | "trib" | "subtrib" | "subser" |
  "ser" | "subgen" | "subg" | "fam" | "subfam" |
  "supertrib") ~ ("." | &(SpaceCharEOI)) }

RankUninomialNotho = _{ ("notho" ~ Sp? ~ ("sect" | "gen" | "ser" | "subgeen" |
  "subgen" | "subg" | "subsect" | "subtrib")) ~ ("." | &(SpaceCharEOI)) }

Uninomial = { UninomialWord ~ (Sp ~ Authorship)? }

UninomialWord = { CapWord | TwoLetterGenus }

CapWord = _{ CapWordWithDash | CapWord1 }

CapWordWithDash = _{ CapWord1 ~ Dash ~ ( UpperAfterDash | LowerAfterDash) }

UpperAfterDash = _{ CapWord1 }

LowerAfterDash = _{ Word1 }

TwoLetterGenus = _{ "Ca" | "Ea" | "Ge" | "Ia" | "Io" | "Ix" | "Lo" | "Oa" |
  "Ra" | "Ty" | "Ua" | "Aa" | "Ja" | "Zu" | "La" | "Qu" | "As" | "Ba" }

Word1 = _{ (ASCII_ALPHA_LOWER ~ Dash)? ~ NameLowerChar ~ NameLowerChar+ }

CapWord1 = _{ NameUpperChar ~ NameLowerChar ~ NameLowerChar+ ~ "?"? }

NameUpperChar = _{ UpperChar | UpperCharExtended }

NameLowerChar = _{ LowerChar | LowerCharExtended | MiscodedChar }

Authorship = { (AuthorshipCombo | OriginalAuthorship) ~
               &(SpaceCharEOI | ";" | ",") }

AuthorshipCombo = { OriginalAuthorshipComb ~ (Sp? ~ CombinationAuthorship)? }

OriginalAuthorship = { AuthorsGroup }

OriginalAuthorshipComb = { BasionymAuthorshipYearMisformed |
                           BasionymAuthorship |
                           BasionymAuthorshipMissingParens }

CombinationAuthorship = { AuthorsGroup }

BasionymAuthorshipMissingParens = { MissingParensStart | MissingParensEnd }

MissingParensStart = _{ "(" ~ Sp? ~ AuthorsGroup }

MissingParensEnd = _{ AuthorsGroup ~ Sp? ~ ")" }

BasionymAuthorshipYearMisformed = _{ "(" ~ Sp? ~ AuthorsGroup ~ Sp? ~
                                     ")" ~ (Sp? ~ ",")? ~ Sp? ~ Year }

BasionymAuthorship = { BasionymAuthorship1 | BasionymAuthorship2Parens }

BasionymAuthorship1 = _{ "(" ~ Sp? ~ AuthorsGroup ~ Sp? ~ ")" }

BasionymAuthorship2Parens = _{ "(" ~ Sp? ~ "(" ~ Sp? ~ AuthorsGroup ~
                              Sp? ~ ")" ~ Sp? ~ ")" }

AuthorsGroup = { AuthorsTeam ~ (Sp ~ (AuthorEmend | AuthorEx) ~ AuthorsTeam)? }

AuthorsTeam = { Author ~ (AuthorSep ~ Author)* ~ (Sp? ~ ","? ~ Sp? ~ Year)? }

AuthorSep = _{ AuthorSep1 | AuthorSep2 }

AuthorSep1 = _{ Sp? ~ ("," ~ Sp)? ~ ( "&" | "et" | "and" | "apud") ~ Sp? }

AuthorSep2 = _{ Sp? ~ "," ~ Sp? }

AuthorEx = _{ ("ex" ~ "."? | "in") ~ Sp }

AuthorEmend = _{ "emend" ~ "."? ~ Sp }

Author = { Author1 | Author2 | UnknownAuthor }

Author1 = _{ Author2 ~ Sp? ~ (Filius | AuthorSuffix) }

Author2 = _{ AuthorWord ~ (Sp? ~ AuthorWord)* }

UnknownAuthor = _{ "?" | (("auct" | "anon") ~ (&(SpaceCharEOI) | ".")) }

AuthorWord = { !("bold:") ~ (AuthorEtAl | AuthorWord2 | AuthorWord3 |
               AuthorPrefix) }

AuthorEtAl = _{ "arg." | "et al." | ("et" | "&") ~ " al" ~ "."? }

AuthorWord2 = _{ AuthorWord3 ~ Dash ~ AuthorWordSoft }

AuthorWord3 = _{ AuthorPrefixGlued? ~ (AllCapsAuthorWord | CapAuthorWord) ~ "."? }

AuthorWordSoft = _{ ((AuthorUpperChar ~ (AuthorUpperChar+ | AuthorLowerChar+)) |
  AuthorLowerChar+) ~ "."? }

CapAuthorWord = _{ AuthorUpperChar ~ AuthorLowerChar* }

AllCapsAuthorWord = _{ AuthorUpperChar ~ AuthorUpperChar+ }

Filius = { "f." | "fil." | "filius" }

AuthorSuffix = _{ "bis" }

AuthorPrefixGlued = _{ ("d" | "O" | "L" | "Mc" | "M") ~ Apostrophe }

AuthorPrefix = _{ AuthorPrefix1 | AuthorPrefix2 }

AuthorPrefix1 = _{ ("ab" | "af" | "bis" | "da" | "der" | "des" |
  "den" | "del" | "della" | "dela" | "de" | "di" | "du" | "el" | "la" |
  "le" | "ter" | "van" | "d" ~ Apostrophe | "in" ~ Apostrophe ~ "t" |
  "zur" | ("von" ~ (Sp ~ ("d."| "dem"))?) | ("v" ~ (Sp ~ "d")?)) ~ &Sp }

AuthorPrefix2 = _{ ("v." ~ (Sp? ~ "d.")?) | Apostrophe ~ "t" }

AuthorUpperChar = _{UpperChar | MiscodedChar |
  ("À" | "Á" | "Â" | "Ã" | "Ä" | "Å" |"Æ" |"Ç" |"È" |"É" |"Ê" |"Ë" |"Ì" |
   "Í" |"Î" |"Ï" |"Ð" |"Ñ" |"Ò" |"Ó" |"Ô" |"Õ" |"Ö" |"Ø" |"Ù" |"Ú" |"Û" |
   "Ü" |"Ý" |"Ć" |"Č" |"Ď" |"İ" |"Ķ" |"Ĺ" |"ĺ" |"Ľ" |"ľ" |"Ł" |"ł" |"Ņ" |
   "Ō" |"Ő" |"Œ" |"Ř" |"Ś" |"Ŝ" |"Ş" |"Š" |"Ÿ" |"Ź" |"Ż" |"Ž" |"ƒ" |"Ǿ" |
   "Ș" |"Ț") }

AuthorLowerChar = _{LowerChar | MiscodedChar |
  ("à" |"á" |"â" |"ã" |"ä" |"å" |"æ" |"ç" |"è" |"é" |"ê" |"ë" |"ì" |"í" |
   "î" |"ï" |"ð" |"ñ" |"ò" |"ó" |"ó" |"ô" |"õ" |"ö" |"ø" |"ù" |"ú" |"û" |
   "ü" |"ý" |"ÿ" |"ā" |"ă" |"ą" |"ć" |"ĉ" |"č" |"ď" |"đ" |"'" |"ē" |"ĕ" |
   "ė" |"ę" |"ě" |"ğ" |"ī" |"ĭ" |"İ" |"ı" |"ĺ" |"ľ" |"ł" |"ń" |"ņ" |"ň" |
   "ŏ" |"ő" |"œ" |"ŕ" |"ř" |"ś" |"ş" |"š" |"ţ" |"ť" |"ũ" |"ū" |"ŭ" |"ů" |
   "ű" |"ź" |"ż" |"ž" |"ſ" |"ǎ" |"ǔ" |"ǧ" |"ș" |"ț" |"ȳ" |"ß") }

Year = { YearRange | YearApprox | YearWithParens | YearWithPage | YearWithDot |
  YearWithChar | YearNum }

YearRange = { YearNum ~ Dash ~ (ASCII_DIGIT+ ~ ("a" | "b" | "c" | "d" |
      "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" |
      "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "?")*) }

YearWithDot =  { YearNum ~ "." }

YearApprox  = { "[" ~ Sp? ~ YearNum ~ Sp? ~ "]" }

YearWithPage = { (YearWithChar | YearNum) ~ Sp? ~ ":" ~ "?" ~ ASCII_DIGIT+ }

YearWithParens = { "(" ~ (YearWithChar | YearNum) ~ ")" }

YearWithChar = { YearNum ~ LowerChar }

YearNum = { ("1" | "2") ~ ("0" | "7" | "8" | "9") ~ ASCII_DIGIT ~ (ASCII_DIGIT | "?") ~ "?"* }

Apostrophe = _{ ApostrOther | ApostrASCII }

ApostrASCII = _{ "'" }

ApostrOther = _{ "‘" | "’" }

Dash = _{ "-" }

MiscodedChar = _{ "�" }

UpperCharExtended = _{ "Æ" | "Œ" | "Ö" }

UpperChar = _{ ASCII_ALPHA_UPPER }

LowerCharExtended = _{ "æ" | "œ" | "à" | "â" | "å" | "ã" | "ä" | "á" | "ç" |
                         "č" | "é" | "è" | "ë" | "í" | "ì" | "ï" | "ň" | "ñ" |
                         "ñ" | "ó" | "ò" | "ô" | "ø" | "õ" | "ö" | "ú" | "ù" |
                         "ü" | "ŕ" | "ř" | "ŗ" | "ſ" | "š" | "š" | "ş" | "ž" }

LowerChar = _{ ASCII_ALPHA_LOWER }

SpaceCharEOI = _{ Sp | EOI }

Sp = _{ MultipleSpace | SingleSpace }

MultipleSpace = _{ SingleSpace ~ SingleSpace+ }

SingleSpace = _{ " " | OtherSpace }

OtherSpace = _{ "　" | " " | "\t" | "\r" | "\n" } //| "\f" | "\v" }
